// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {VRFCoordinatorV2Interface} from "@chainlink/contracts/src/v0.8/vrf/interfaces/VRFCoordinatorV2Interface.sol";
import {VRFConsumerBaseV2} from "@chainlink/contracts/src/v0.8/vrf/VRFConsumerBaseV2.sol";
import {StructDefinition} from "../utils/library/StructDefinition.sol";
import {VerifierInterface} from "../utils/interface/VerifierInterface.sol";

/**
 * @title Distribution contract for generating random numbers for verifiers
 * @author Luo Yingjie
 * @notice This contract is for generating random numbers for verifiers
 * @dev The random number is generated by Chainlink VRF, scripts alreadly developed, don't forget to update the subscriptionId in HelperConfig.s.sol
 */
contract Distribution is VRFConsumerBaseV2 {
    /**
     * @dev StrcutDefinition is used for defining the struct of DistributionVerifierRequestContext and VSkillUserEvidence
     */
    using StructDefinition for StructDefinition.DistributionVerifierRequestContext;
    using StructDefinition for StructDefinition.VSkillUserEvidence;

    /**
     * @dev Those variables are used for Chainlink VRF
     */
    uint64 subscriptionId;
    VRFCoordinatorV2Interface vrfCoordinator;
    bytes32 keyHash;
    uint32 callbackGasLimit;
    uint16 requestConfirmations = 3;
    uint32 numWords = 3;

    uint256 requestId;

    uint256[] private randomWords;
    mapping(uint256 => StructDefinition.DistributionVerifierRequestContext)
        private requestIdToContext;

    event RequestIdToContextUpdated(
        uint256 indexed requestId,
        StructDefinition.DistributionVerifierRequestContext context
    );

    constructor(
        uint64 _subscriptionId,
        address _vrfCoordinator,
        bytes32 _keyHash,
        uint32 _callbackGasLimit
    ) VRFConsumerBaseV2(_vrfCoordinator) {
        subscriptionId = _subscriptionId;
        vrfCoordinator = VRFCoordinatorV2Interface(_vrfCoordinator);
        keyHash = _keyHash;
        callbackGasLimit = _callbackGasLimit;
    }

    /**
     *
     * @param requester The address of the requester, here is the `Verifier` conrtact
     * @param ev The evidence of the user, which is to be distributed to the verifiers
     */
    function distributionRandomNumberForVerifiers(
        address requester,
        StructDefinition.VSkillUserEvidence memory ev
    ) public {
        requestId = vrfCoordinator.requestRandomWords(
            keyHash,
            subscriptionId,
            requestConfirmations,
            callbackGasLimit,
            numWords
        );

        requestIdToContext[requestId] = StructDefinition
            .DistributionVerifierRequestContext(requester, ev);

        emit RequestIdToContextUpdated(
            requestId,
            requestIdToContext[requestId]
        );
    }

    ///////////////////////////////
    /////   Internal Functions ////
    ///////////////////////////////

    /**
     *
     * @param _randomWords The random words generated by Chainlink VRF for further processing
     * @dev This function is called by Chainlink VRF to fulfill the request of random numbers
     */
    function fulfillRandomWords(
        uint256 /*_requestId*/,
        uint256[] memory _randomWords
    ) internal override {
        randomWords = _randomWords;
        _processVerifiers(requestId);
    }

    /**
     *
     * @param _requestId The requestId of the Chainlink VRF
     * @dev This function will use the `VerifierInterface` to process the random words.
     */
    function _processVerifiers(uint256 _requestId) internal {
        StructDefinition.DistributionVerifierRequestContext
            memory context = requestIdToContext[_requestId];
        VerifierInterface(context.requester)._selectedVerifiersAddressCallback(
            context.ev,
            randomWords
        );
    }

    ///////////////////////////////
    /////   Getter Functions   ////
    ///////////////////////////////

    function getRandomWords() public view returns (uint256[] memory) {
        return randomWords;
    }

    function getRequestIdToContext(
        uint256 _requestId
    )
        public
        view
        returns (StructDefinition.DistributionVerifierRequestContext memory)
    {
        return requestIdToContext[_requestId];
    }

    function getSubscriptionId() public view returns (uint64) {
        return subscriptionId;
    }

    function getVrfCoordinator()
        public
        view
        returns (VRFCoordinatorV2Interface)
    {
        return vrfCoordinator;
    }

    function getKeyHash() public view returns (bytes32) {
        return keyHash;
    }

    function getCallbackGasLimit() public view returns (uint32) {
        return callbackGasLimit;
    }

    function getRequestConfirmations() public view returns (uint16) {
        return requestConfirmations;
    }
}
