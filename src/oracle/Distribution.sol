// SPDX-License-Identifier: MIT

// @audit-info floating pragma
pragma solidity ^0.8.24;

import {VRFCoordinatorV2Interface} from "@chainlink/contracts/src/v0.8/vrf/interfaces/VRFCoordinatorV2Interface.sol";
import {VRFConsumerBaseV2} from "@chainlink/contracts/src/v0.8/vrf/VRFConsumerBaseV2.sol";
import {StructDefinition} from "../utils/library/StructDefinition.sol";
import {VerifierInterface} from "../utils/interface/VerifierInterface.sol";

/**
 * @title Distribution contract for generating random numbers for verifiers
 * @author Luo Yingjie
 * @notice This contract is for generating random numbers for verifiers
 * @dev The random number is generated by Chainlink VRF, scripts alreadly developed, don't forget to update the subscriptionId in HelperConfig.s.sol
 */

// q is this VRF v2 good?
contract Distribution is VRFConsumerBaseV2 {
    /**
     * @dev StrcutDefinition is used for defining the struct of DistributionVerifierRequestContext and VSkillUserEvidence
     */
    using StructDefinition for StructDefinition.DistributionVerifierRequestContext;
    using StructDefinition for StructDefinition.VSkillUserEvidence;

    /**
     * @dev Those variables are used for Chainlink VRF
     */
    uint64 s_subscriptionId;
    VRFCoordinatorV2Interface s_vrfCoordinator;
    bytes32 s_keyHash;
    uint32 s_callbackGasLimit;
    uint16 s_requestConfirmations = 3;
    uint32 s_numWords = 3;

    uint256 s_requestId;

    uint256[] private s_randomWords;
    mapping(uint256 => StructDefinition.DistributionVerifierRequestContext)
        private s_requestIdToContext;

    event RequestIdToContextUpdated(
        uint256 indexed requestId,
        StructDefinition.DistributionVerifierRequestContext context
    );

    constructor(
        uint64 _subscriptionId,
        address _vrfCoordinator,
        bytes32 _keyHash,
        uint32 _callbackGasLimit
    ) VRFConsumerBaseV2(_vrfCoordinator) {
        s_subscriptionId = _subscriptionId;
        s_vrfCoordinator = VRFCoordinatorV2Interface(_vrfCoordinator);
        s_keyHash = _keyHash;
        s_callbackGasLimit = _callbackGasLimit;
    }

    /**
     *
     * @param requester The address of the requester, here is the `Verifier` conrtact
     * @param ev The evidence of the user, which is to be distributed to the verifiers
     */

    // q can anyone call this function to distribute random numbers? Any impact?
    function distributionRandomNumberForVerifiers(
        address requester,
        StructDefinition.VSkillUserEvidence memory ev
    ) public {
        s_requestId = s_vrfCoordinator.requestRandomWords(
            s_keyHash,
            s_subscriptionId,
            s_requestConfirmations,
            s_callbackGasLimit,
            s_numWords
        );

        s_requestIdToContext[s_requestId] = StructDefinition
            .DistributionVerifierRequestContext(requester, ev);

        emit RequestIdToContextUpdated(
            s_requestId,
            s_requestIdToContext[s_requestId]
        );
    }

    ///////////////////////////////
    /////   Internal Functions ////
    ///////////////////////////////

    /**
     *
     * @param _randomWords The random words generated by Chainlink VRF for further processing
     * @dev This function is called by Chainlink VRF to fulfill the request of random numbers
     */
    function fulfillRandomWords(
        uint256 /*_requestId*/,
        uint256[] memory _randomWords
    ) internal override {
        // q why store random words in a state variable? Anyone can access this state variable? If they can see this random words, any impact?
        s_randomWords = _randomWords;
        _processVerifiers(s_requestId);
    }

    /**
     *
     * @param _requestId The requestId of the Chainlink VRF
     * @dev This function will use the `VerifierInterface` to process the random words.
     */
    function _processVerifiers(uint256 _requestId) internal {
        StructDefinition.DistributionVerifierRequestContext
            memory context = s_requestIdToContext[_requestId];
        // q is this _selectedVerifiersAddressCallback function in Verifier contract?
        VerifierInterface(context.requester)._selectedVerifiersAddressCallback(
            context.ev,
            s_randomWords
        );
    }

    ///////////////////////////////
    /////   Getter Functions   ////
    ///////////////////////////////

    function getRandomWords() public view returns (uint256[] memory) {
        return s_randomWords;
    }

    function getRequestIdToContext(
        uint256 _requestId
    )
        public
        view
        returns (StructDefinition.DistributionVerifierRequestContext memory)
    {
        return s_requestIdToContext[_requestId];
    }

    function getSubscriptionId() public view returns (uint64) {
        return s_subscriptionId;
    }

    function getVrfCoordinator()
        public
        view
        returns (VRFCoordinatorV2Interface)
    {
        return s_vrfCoordinator;
    }

    function getKeyHash() public view returns (bytes32) {
        return s_keyHash;
    }

    function getCallbackGasLimit() public view returns (uint32) {
        return s_callbackGasLimit;
    }

    function getRequestConfirmations() public view returns (uint16) {
        return s_requestConfirmations;
    }
}
